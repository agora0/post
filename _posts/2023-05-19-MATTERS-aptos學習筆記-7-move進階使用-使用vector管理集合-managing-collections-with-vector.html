---
layout: post
title: "[Aptos學習筆記#7]Move進階使用 -  使用Vector管理集合 - Managing Collections with Vector"
date: 2023-05-19 09:19:42.000000000 +00:00
link: https://matters.news/@chwang12341/aptos%E5%AD%B8%E7%BF%92%E7%AD%86%E8%A8%98-7-move%E9%80%B2%E9%9A%8E%E4%BD%BF%E7%94%A8-%E4%BD%BF%E7%94%A8vector%E7%AE%A1%E7%90%86%E9%9B%86%E5%90%88-managing-collections-with-vector-bafybeieyrxwzqfyjuy4h3weluynkfuute2robot7x3o5tchnrtm4hlefzi
categories: matters
tags: blog
author: 為自己Coding
---

<figure class="image"><img src="https://assets.matters.news/embed/bd255f0b-5119-40c6-9846-16c0b5235606.jpeg" data-asset-id="bd255f0b-5119-40c6-9846-16c0b5235606" referrerpolicy="no-referrer"><figcaption><span>攝影師：Pixabay: https://www.pexels.com/zh-tw/photo/290470/</span></figcaption></figure><p><br></p><h2>目錄</h2><ol><li><strong>為什麼需要Vector?</strong></li><li><strong>Vector是什麼?</strong></li><li><strong>內聯 Vector 定義的十六進制數組與字符 - Hex and Bytestring literal for inline vector definitions</strong></li><li><strong>Vector 速查表 - Vector Cheatsheet</strong></li></ol><p><br></p><h2>一. 為什麼需要Vector?</h2><p>前面的筆記中我們已經很熟悉結構體的類型了，它讓我們可以創建自己的類型並儲存複雜的數據，但<strong>有時候我們需要更動態、可擴展性核可管理性的功能，這邊Move提供了向量Vector的概念來處理</strong></p><h2><br></h2><h2>二. Vector是什麼?</h2><ul><li>用於儲存數據集合的內置類型( build-in type)</li><li>集合數據能夠是任何類型(但僅一種)</li><li>Vector的功能實際上是由Move VM提供的</li><li>使用Vector唯一的辦法就是使用Move標準庫和native函數</li></ul><p><br></p><pre class="ql-syntax" spellcheck="false">script {
    use 0x1::Vector;

    fun main() {
        // 使用泛型創建一個空的 Vector
        let a = Vector::empty<u8>();
        let i = 0;

        // 將值塞入剛剛創建的Vector中
        while (i < 10) {
            Vector::push_back(&mut a, i);
            i = i + 1;
        };

        // 現在印出Vector的長度
        let a_len = Vector::length(&a);
        0x1::Debug::print<u64>(&a_len);

        // 移除掉兩個元素
        Vector::pop_back(&mut a);
        Vector::pop_back(&mut a);

        // 再次印出Vector長度
        let a_len = Vector::length(&a);
        0x1::Debug::print<u64>(&a_len);
    }
}
</pre><p>Vector最多可以儲存U64個單個非引用類型(single non-reference type)的值</p><p><br></p><p>例子: 為了理解Vector如何幫助管理巨大的存儲量，這邊撰寫一個模塊 ( Module )</p><pre class="ql-syntax" spellcheck="false">module Shelf {

    use 0x1::Vector;

    struct Box<T> {
        value: T
    }

    struct Shelf<T> {
        boxes: vector<Box<T>>
    }

    public fun create_box<T>(value: T): Box<T> {
        Box { value }
    }

    // 對於不可複製的內容本方法不可行
    public fun value<T: copy>(box: &Box<T>): T {
        *&box.value
    }

    public fun create<T>(): Shelf<T> {
        Shelf {
            boxes: Vector::empty<Box<T>>()
        }
    }

    // 將Box的值移入Vector
    public fun put<T>(shelf: &mut Shelf<T>, box: Box<T>) {
        Vector::push_back<Box<T>>(&mut shelf.boxes, box);
    }
    
    // 將Vector中的元素移出
    public fun remove<T>(shelf: &mut Shelf<T>): Box<T> {
        Vector::pop_back<Box<T>>(&mut shelf.boxes)
    }

    // 計算Vector的長度
    public fun size<T>(shelf: &Shelf<T>): u64 {
        Vector::length<Box<T>>(&shelf.boxes)
    }
}
</pre><p><br></p><p>在Script中，我們首先創建一個 Shelf，並為其提供幾個 Box，並觀察如何在Module中使用 Vector</p><pre class="ql-syntax" spellcheck="false">script {
    use {{sender}}::Shelf;

    fun main() {

        // 創建一個shelf和兩個u64類型的Box
        let shelf = Shelf::create<u64>();
        let box_1 = Shelf::create_box<u64>(99);
        let box_2 = Shelf::create_box<u64>(999);

        // 將剛剛創建的兩個Box推進Vector
        Shelf::put(&mut shelf, box_1);
        Shelf::put(&mut shelf, box_2);

        // 印出長度 - 結果為2
        0x1::Debug::print<u64>(&Shelf::size<u64>(&shelf));

        // 拿走最後一個推進Vector的Box
        let take_back = Shelf::remove(&mut shelf);
        let value     = Shelf::value<u64>(&take_back);

        // 驗證我們拿出來的Box值是999
        assert(value == 999, 1);

        // 再次印出Vector長度 - 結果為1
        0x1::Debug::print<u64>(&Shelf::size<u64>(&shelf));
    }
}
</pre><p>Vector非常強大，它允許我們存儲大量數據(最大長度為 18,446,744,073,709,551,615)，並讓我們在索引的存儲中使用它</p><h2><br></h2><h2>三. 內聯 Vector 定義的十六進制數組與字符 - Hex and Bytestring literal for inline vector definitions</h2><p>Vector也代表著表示字符串</p><p>VM 支援將vector<u8>當作參數傳遞給腳本中的主函數的方式</p><p><br></p><p>例子: 我們也可以在使用十六進制文本在Script或Module中定義一個vector<u8></p><pre class="ql-syntax" spellcheck="false">script {

    use 0x1::Vector;

    // 這是在main中傳入參數的方式
    fun main(name: vector<u8>) {
        let _ = name;

        // 這是我們使用文字的方式
        // 這邊表示的是一個hello world的字符串
        let str = x"68656c6c6f20776f726c64";

        // 十六進制的文字也傳給我們vector<u8>
        Vector::length<u8>(&str);
    }
}
</pre><p><br></p><p>有個簡單的方式就是使用字節串文字(bytestring literals)</p><pre class="ql-syntax" spellcheck="false">script {

    fun main() {
        // 使用b"<我們想輸入的字符串>"的方式更簡易
        let _ = b"hello world";
    }
}
</pre><p>它們被當成是ASCII 字符串，也被表示為vector<u8></p><h2><br></h2><h2>四. Vector 速查表 - Vector Cheatsheet</h2><p>在標準庫中的簡短Vector方法速查表</p><p>1.創建一個類型為<E>的空Vector</p><pre class="ql-syntax" spellcheck="false">Vector::empty<E>(): vector<E>;
</pre><p>2.取得Vector長度</p><pre class="ql-syntax" spellcheck="false">Vector::length<E>(v: &vector<E>): u64;
</pre><p>3.將元素推向Vector的末端</p><pre class="ql-syntax" spellcheck="false">Vector::push_back<E>(v: &mut vector<E>, e: E);
</pre><p>4.獲取對Vector的可變引用，不可變引用可以使用Vector::borrow()</p><pre class="ql-syntax" spellcheck="false">Vector::borrow_mut<E>(v: &mut vector<E>, i: u64): &E;
</pre><p>5.從Vector末端取走一個元素</p><pre class="ql-syntax" spellcheck="false">Vector::pop_back<E>(v: &mut vector<E>): E;
</pre><p><br></p><p><strong>標準庫中的Vector模塊:</strong></p><p><a href="https://github.com/diem/move/blob/main/language/move-stdlib/sources/Vector.move" rel="noopener noreferrer" target="_blank">move/Vector.move at main · diem/move</a></p>
